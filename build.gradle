import org.flywaydb.gradle.task.FlywayMigrateTask

buildscript {
    ext {
        springBootVersion = "2.2.2.RELEASE"
    }

    repositories {
        maven { url "http://platform-acceleration-lab-maven.s3.amazonaws.com" }
        mavenCentral()
    }

    dependencies {
        classpath "io.pivotal.pal.assignments:assignments-plugin:4.3.1"
        classpath "org.springframework.boot:spring-boot-gradle-plugin:$springBootVersion"
    }
}


plugins {
    id "java"
    id "org.flywaydb.flyway" version "5.2.4"
}

apply plugin: 'org.springframework.boot'
apply plugin: "io.pivotal.pal.assignments.plugin"

assignments {
    apiUrl = "https://waveland.education.pivotal.io/"
    emails = ["srastogi@pivotal.io"]
    cohortIdentifier = "pivotal-labs"
}

repositories {
    mavenCentral()
}

dependencies {
    compile("org.springframework.boot:spring-boot-starter-web:$springBootVersion")
    compile("org.springframework.boot:spring-boot-starter-jdbc:$springBootVersion")
    compile("org.springframework.boot:spring-boot-starter-actuator:$springBootVersion")
    compile("mysql:mysql-connector-java:8.0.18")
    testCompile("org.springframework.boot:spring-boot-starter-test:$springBootVersion")
}

def developmentDbUrl = "jdbc:mysql://localhost:3306/tracker_dev?user=tracker&useSSL=false&useTimezone=true&serverTimezone=UTC&useLegacyDatetimeCode=false"
bootRun.environment([
        "WELCOME_MESSAGE"                          : "Hello",
        "SPRING_DATASOURCE_URL"                    : developmentDbUrl,
        "MANAGEMENT_ENDPOINTS_WEB_EXPOSURE_INCLUDE": "*",
        "MANAGEMENT_ENDPOINT_HEALTH_SHOWDETAILS"   : "always",
])

def testDbUrl = "jdbc:mysql://localhost:3306/tracker_test?user=tracker&useSSL=false&useTimezone=true&serverTimezone=UTC&useLegacyDatetimeCode=false"
test.environment([
        "WELCOME_MESSAGE"                          : "Hello from test",
        "SPRING_DATASOURCE_URL"                    : testDbUrl,
        "MANAGEMENT_ENDPOINTS_WEB_EXPOSURE_INCLUDE": "*",
        "MANAGEMENT_ENDPOINT_HEALTH_SHOWDETAILS"   : "always",
])

flyway {
    url = developmentDbUrl
    user = "tracker"
    password = ""
    locations = ["filesystem:databases/tracker/migrations"]
}

task testMigrate(type: FlywayMigrateTask) {
    url = testDbUrl
}

springBoot {
    buildInfo()
}